// 2013-09-12 Yu Yahagi
// Test simulation of a 3D beam with two physical volume with different
// material parameters.

load "msh3"
load "medit"

//////////////////////////////////////////////////////////////////////////////
// Material parameters
//////////////////////////////////////////////////////////////////////////////
mesh3	Th = readmesh3("beam_2vol.mesh");
mesh3 Thm;
plot(Th, wait=1);
string	filename = "test_beam.txt";
real E1 = 1.85e-22, nu1 = 0.28; // E [kg/(nm ps^2)] for the ellipse
real rho1 = 2.3290e-24;       // rho [kg/nm^3]
real lambda1 = E1*nu1/((1+nu1)*(1-2*nu1));
real mu1 = E1/(2*(1+nu1));
real E2 = 1.85e-22, nu2 = 0.28; // E [kg/(nm ps^2)] for the substrate
real rho2 = 2.3290e-24;       // rho [kg/nm^3]
real lambda2 = E2*nu2/((1+nu2)*(1-2*nu2));
real mu2 = E2/(2*(1+nu2));

//////////////////////////////////////////////////////////////////////////////
// Finite Element spaces, functions, and macros
//////////////////////////////////////////////////////////////////////////////
fespace Vh(Th, [P1,P1,P1]);
fespace VhScalar(Th, P1);
Vh [u1,u2,u3], [v1,v2,v3], [uu1,uu2,uu3], [uuu1,uuu2,uuu3];
Vh [ur1,ur2,ur3];			// For visualization
Vh [f1,f2,f3] = [0.0, 0.0, 0.0];	// Boundary condition at the bottom
VhScalar uAbs, ur, uphi;

real gravity = -0.1;

real sqrt2=sqrt(2.);
macro epsilon(u1,u2,u3)  [dx(u1),dy(u2),dz(u3),(dz(u2)+dy(u3))/sqrt2,(dz(u1)+dx(u3))/sqrt2,(dy(u1)+dx(u2))/sqrt2] // EOM
macro div(u1,u2,u3) ( dx(u1)+dy(u2)+dz(u3) ) // EOM

real dmax1, dmax2, coef;
int[int] ref2=[1,0,2,0];

// Surface force between the volumes
// Defined for the volume 2 (bottom one) using the normal vector of [0,0,1]
// Volume 1 (top) feels the surface force of -[t1,t2,t3] at the interface
Vh [t1,t2,t3];
macro surface_force(u1,u2,u3) [mu*(dz(u1)+dx(u3)),mu*(dy(u3)+dz(u2)),lambda*div(u1,u2,u3)+2*mu*dz(u3)]

//////////////////////////////////////////////////////////////////////////////
// 1 Volume
//////////////////////////////////////////////////////////////////////////////

solve beam1([u1,u2,u3],[v1,v2,v3]) =
	int3d(Th)(
		lambda1*div(u1,u2,u3)*div(v1,v2,v3) + 2.*mu1*epsilon(u1,u2,u3)'*epsilon(v1,v2,v3)
	)
  - int3d(Th) (gravity*v3)
  + on(1, u1=0, u2=0, u3=0)
;

dmax1 = u1[].max;
cout << "Max displacement = " << dmax1 << endl;
coef = 0.4/dmax1;
Thm=movemesh3(Th,transfo=[x+u1*coef,y+u2*coef,z+u3*coef],label=ref2);
Thm=change(Thm,label=ref2);
plot(Th,Thm, wait=1,cmm="coef  amplification = "+coef );

//////////////////////////////////////////////////////////////////////////////
// Problem
//////////////////////////////////////////////////////////////////////////////
solve beam2([u1,u2,u3],[v1,v2,v3]) =
	int3d(Th,1)(
		lambda1*div(u1,u2,u3)*div(v1,v2,v3) + 2.*mu1*epsilon(u1,u2,u3)'*epsilon(v1,v2,v3)
	)
	+ int3d(Th,2)(
		lambda2*div(u1,u2,u3)*div(v1,v2,v3) + 2.*mu2*epsilon(u1,u2,u3)'*epsilon(v1,v2,v3)
	)
  - int3d(Th,1) (gravity*v3)
  - int3d(Th,2) (gravity*v3)
  + on(1, u1=0, u2=0, u3=0)
;

dmax2 = u1[].max;
cout << "Max displacement = " << dmax2 << endl;
Thm=movemesh3(Th,transfo=[x+u1*coef,y+u2*coef,z+u3*coef],label=ref2);
Thm=change(Thm,label=ref2);
plot(Th,Thm, wait=1,cmm="coef  amplification = "+coef );

cout << "dmax1 = " << dmax1 << ", dmax2 = " << dmax2 << endl;
